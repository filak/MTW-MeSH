PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh:   <http://id.nlm.nih.gov/mesh/>
PREFIX text:   <http://jena.apache.org/text#>
PREFIX meshx:  <http://mesh.medvik.cz/link/>
PREFIX mesht:  <http://www.medvik.cz/schema/mesh/vocab/#>

SELECT ?dui ?cui ?dtype ?den ?trx ?notrx ?active ?crt ?est ?nlm ?rn ?cas
      (GROUP_CONCAT(DISTINCT ?trns ; separator='~') AS ?trn)
      (GROUP_CONCAT(DISTINCT ?pac ; separator='~') AS ?pa)
      (GROUP_CONCAT(DISTINCT ?bd ; separator='~') AS ?btd)
      (GROUP_CONCAT(DISTINCT ?nd ; separator='~') AS ?ntd)
      (GROUP_CONCAT(DISTINCT ?rd ; separator='~') AS ?rtd)
WHERE {
  #BIND(mesh:D000003 as ?d)
  #BIND(mesh:D012459 as ?d)
  #BIND(mesh:Q000008 as ?d)
  #BIND(mesh:Q000009 as ?d)
  #BIND(mesh:D002493 as ?d)
  #BIND(mesh:D005123 as ?d)
  #BIND(mesh:D018806 as ?d)
  BIND(mesh:D023482 as ?d)
  ?d rdf:type ?dtype .
  FILTER(?dtype IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
  ?d meshv:identifier ?dui .
  ?d meshv:active ?active .
  ?d meshv:dateCreated ?crt .
  ?d meshv:dateEstablished ?est .
  OPTIONAL {
    ?d meshv:treeNumber ?tree .
    ?tree rdfs:label ?trns
  }
  ?d meshv:preferredConcept ?c .
  ?c meshv:identifier ?cui .
  ?c meshv:preferredTerm ?t .
  ?t meshv:prefLabel ?den .
  OPTIONAL {
    ?c mesht:preferredTerm ?tp .
    ?tp mesht:prefLabel ?trx
  }
  OPTIONAL {
    ?d meshv:broaderDescriptor ?bdx .
    ?bdx meshv:identifier ?bd
  }
  OPTIONAL {
    ?ndx meshv:broaderDescriptor ?d .
    ?ndx meshv:identifier ?nd
  }
  OPTIONAL {
    ?d meshv:seeAlso ?rdx .
    ?rdx meshv:identifier ?rd
  }
  OPTIONAL { ?c mesht:notTranslatable ?notrx }
  OPTIONAL { ?d meshv:nlmClassificationNumber ?nlm }
  OPTIONAL {
    ?d meshv:pharmacologicalAction ?phac .
    ?phac meshv:identifier ?pac
  }
  OPTIONAL { ?c meshv:registryNumber ?rn }
  OPTIONAL { ?c meshv:casn1_label ?cas }
}
GROUP BY ?dui ?cui ?dtype ?den ?trx ?notrx ?active ?crt ?est ?nlm ?rn ?cas
###LIMIT 100
