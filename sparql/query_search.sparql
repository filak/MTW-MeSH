PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh:   <http://id.nlm.nih.gov/mesh/>
PREFIX text:   <http://jena.apache.org/text#>
PREFIX meshx:  <http://mesh.medvik.cz/link/>
PREFIX mesht:  <http://www.medvik.cz/schema/mesh/vocab/#>

SELECT distinct ?label ?d ?type ?active ?val ?scn ?scnt ?ntx ?lockedBy {
{
SELECT distinct ?d ?type
WHERE {

      (?s ?score) text:query (mesht:queryDef "eye" 500) .

  OPTIONAL {
    BIND(?s as ?d)
    ?s rdf:type ?type .
    ?s rdfs:label ?label .
    FILTER(?type IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
    }
  OPTIONAL {
    ?d meshv:treeNumber ?s .
    ?d rdf:type ?type .
    ?d rdfs:label ?label .
    }
  OPTIONAL {
    ?c meshv:preferredTerm|meshv:term ?s .
    ?d meshv:preferredConcept|meshv:concept ?c .
    ?d rdf:type ?type .
    FILTER(?type IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
    ?c rdf:type ?ctype .
    FILTER(?ctype NOT IN(meshv:SCR_Anatomy, meshv:SCR_Disease, meshv:SCR_Chemical, meshv:SCR_Organism, meshv:SCR_Population, meshv:SCR_Protocol))
    }
  OPTIONAL {
    ?s rdf:type ?stype .
    ?d meshv:preferredConcept|meshv:concept ?s .
    ?d rdf:type ?type .
    FILTER(?stype IN(meshv:Concept))
    }
  OPTIONAL {
    ?c mesht:preferredTerm|mesht:term ?s .
    ?d meshv:preferredConcept|meshv:concept ?c .
    ?d rdf:type ?type .
    }
  OPTIONAL {
    ?d mesht:concept ?s .
    ?d rdf:type ?type .
    }
FILTER (BOUND(?d))
FILTER (?type IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
}
ORDER BY DESC(?score)
  }
  ?d rdfs:label ?label .
  FILTER ( lang(?label) = 'en' )
  ?d meshv:preferredConcept ?c .
  # Assume absence of meshv:active as true
  BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?active) .
  OPTIONAL { ?d mesht:lockedBy ?lockedBy }
  OPTIONAL {
    ?c mesht:preferredTerm ?termID .
    ?termID mesht:prefLabel ?val
  }
  OPTIONAL {?c meshv:scopeNote ?scnv .
            BIND('YES' as ?scn)
           }
  OPTIONAL {?c mesht:scopeNote ?scntv
            BIND('YES' as ?scnt)
           }
} LIMIT 500
