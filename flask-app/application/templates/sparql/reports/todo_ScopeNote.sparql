### Not translated ScopeNote in preferred Concepts/Descriptors v1.1 ###

{% include 'sparql/_namespaces.sparql' %}

SELECT distinct ?label ?d ?type ?active ?val ?scn ?scnt ?ntx ?lockedBy
WHERE {
  ?d meshv:preferredConcept ?c .
  ?d rdf:type ?type .
  FILTER(?type IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
  ?d rdfs:label ?label .
  FILTER (lang(?label) = 'en')
  ?c meshv:scopeNote ?scnx .
  BIND('YES' as ?scn)
  ?c mesht:preferredTerm ?id .
  ?id mesht:prefLabel ?val .
  OPTIONAL {
   ?c mesht:scopeNote ?scntv
   BIND('YES' as ?scnt)
   }
   FILTER (!BOUND(?scnt))

  # Assume absence of meshv:active as true
  BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?active) .

  #?d meshv:dateCreated ?dateCrt .
  #OPTIONAL { ?d meshv:dateRevised|lastUpdated ?dateRev }
  OPTIONAL { ?c mesht:notTranslatable ?ntx }
  OPTIONAL { ?d mesht:lockedBy ?lockedBy }
}
ORDER BY ?label LIMIT 200

