## useInstead Descriptor-qualifs export query v1.2

{% include 'sparql/_namespaces.sparql' %}

SELECT ?dui (GROUP_CONCAT(DISTINCT ?o ; separator='|') AS ?qn)
WHERE {
  ?ddq meshv:hasDescriptor ?d .

  # Assume absence of meshv:active as true
  BIND(NOT EXISTS { ?ddq meshv:active false } AS ?ddqactive)
  FILTER(?ddqactive) .

  ?d meshv:identifier ?dui .
  ?ddq meshv:useInstead ?adq .

  BIND(NOT EXISTS { ?adq meshv:active false } AS ?adqactive)
  FILTER(?adqactive) .

  ?ddq meshv:hasQualifier ?q .
  ?q meshv:identifier ?qui .
  BIND( CONCAT(STR(?qui), '~', REPLACE(STR(?adq), str(mesh:), '')) AS ?o)
}
GROUP BY ?dui
#LIMIT 100
