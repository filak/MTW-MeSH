### Search for duplicates in translation v1.1

{% include 'sparql/_namespaces.sparql' %}

SELECT ?trx ?ttype ?cnt ?ids {
{
SELECT ?trx ?ttype (count(*) as ?cnt)
    (GROUP_CONCAT(DISTINCT ?dui ; separator='~') AS ?ids)
WHERE {
  ?d rdf:type ?type .
  FILTER(?type IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag))
  FILTER NOT EXISTS { ?d meshv:active false }
  ?d meshv:identifier ?dui .
  ?d meshv:preferredConcept|meshv:concept ?c .
  ?c mesht:preferredTerm|mesht:term ?t .
  ?t mesht:prefLabel ?val .
  BIND(STR(?val) AS ?trx)
  BIND('descriptors' AS ?ttype)
}
GROUP BY ?trx ?ttype
HAVING (?cnt > 1)
} UNION
{
SELECT ?trx ?ttype (count(*) as ?cnt)
   (GROUP_CONCAT(DISTINCT ?dui ; separator='~') AS ?ids)
WHERE {
  ?d rdf:type ?type .
  FILTER(?type IN(meshv:Qualifier))
  FILTER NOT EXISTS { ?d meshv:active false }
  ?d meshv:identifier ?dui .
  ?d meshv:preferredConcept|meshv:concept ?c .
  ?c mesht:preferredTerm|mesht:term ?t .
  ?t mesht:prefLabel ?val .
  BIND(STR(?val) AS ?trx)
  BIND('qualifiers' AS ?ttype)
}
GROUP BY ?trx ?ttype
HAVING (?cnt > 1)
}
} ORDER BY ?ttype ?trx
